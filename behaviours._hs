behavior Draggable(dragHandle)
  init
    if no dragHandle set the dragHandle to me
  end

  on pointerdown(clientX, clientY) from dragHandle
    halt the event
    trigger draggable:start

    measure my x, y
    set xoff to clientX - x
    set yoff to clientY - y

    repeat until event pointerup from document
      wait for pointermove(pageX, pageY) or
              pointerup  (pageX, pageY) from document
      add { left: ${pageX - xoff}px; top: ${pageY - yoff}px; }
      trigger draggable:move
    end
    trigger draggable:end
end


behavior Resizable(resizeHandle)
  init
    if no resizeHandle set the resizeHandle to me
  end

  on pointerdown(clientX, clientY) from resizeHandle
    halt the event
    trigger resizable:start

    measure my x, y

    repeat until event pointerup from document
      wait for pointermove(pageX, pageY) or
              pointerup  (pageX, pageY) from document
      set xoff to pageX - x
      set yoff to pageY - y
      add { width: ${xoff}px; height: ${yoff}px; }
      trigger resizable:move
    end
    trigger resizable:end
end