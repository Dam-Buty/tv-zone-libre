on loadData
  fetch /data/data.json as json with cache:"no-store" then
    set global channels to result
    template()

def computePlaylistOffset(channel)
  set now to Math.round(Date.now() / 1000)
  set totalOffset to now - channel.startTime
  set playlistDuration to 0

  for video in channel.videos
    set playlistDuration to playlistDuration + video.duration
  end

  return totalOffset mod playlistDuration
end

def zap(id)
  set channel to channels.find( \ channel -> channel.id == id )

  if channel.link exists then
    go to url ${channel.link} in new window
  else
    set global currentChannelId to id
    set videoCursor to 0
    set videoOffset to computePlaylistOffset(channel)

    repeat while videoOffset > channel.videos[videoCursor].duration
      set videoOffset to videoOffset - channel.videos[videoCursor].duration
      set videoCursor to videoCursor + 1
    end

    set #tv-frame.src to channel.videos[videoCursor].url
    set #tv-frame.currentTime to videoOffset
    put `↜ ${channel.videos[videoCursor].title} ↝` into #tv-handle
  end
end

def template()
  for channel in channels
    if channel.link exists
      put `
        <a href="${channel.link}" target="_blank" class="block">
          <div
            class="card-item bg-light text-dark shadow-md rounded-lg p-6 cursor-pointer min-h-[500px] max-h-[500px] flex flex-col justify-between"
          >
            <div>
              <h2 class="text-2xl font-bold mb-2">${channel.title}</h2>
              <img class="w-full h-64 object-cover rounded-t-lg" src="${channel.image}" alt="${channel.title}">
              <p class="bg-sand text-gray-600 mt-2 p-4 border-dotted border-2 border-dark rounded-b-lg">${channel.description}</p>
            </div>
            <aside class="cursor-alias text-right text-sm my-2">
              Cette chaîne n'est pas hébergée sur Télé Zone Libre ⇾
            </aside>
          </div>
        </a>
      `
      at the end of #card-container
    else
      put `
        <div
          class="card-item bg-light text-dark shadow-md rounded-lg p-6 cursor-pointer min-h-[500px] max-h-[500px] flex flex-col justify-between"
          _="on click zap('${channel.id}')"
        >
          <div>
            <h2 class="text-2xl font-bold mb-2">${channel.title}</h2>
            <img class="w-full h-64 object-cover rounded-t-lg" src="${channel.image}" alt="${channel.title}">
            <p class="bg-sand text-gray-600 mt-2 p-4 border-dotted border-2 border-dark rounded-b-lg">${channel.description}</p>
          </div>
        </div>
      `
      at the end of #card-container
    end
  end

  send click to the first <div/> in #card-container
end